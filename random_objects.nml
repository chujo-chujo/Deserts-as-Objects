
// // Random oases at mapgen
spritelayout layout_oasis_menu {
    ground   { sprite: spriteset_text(6); } }

spritelayout layout_oasis_T1 {
    ground   { sprite: GROUNDSPRITE_DESERT_1_2; }
    building { sprite: spriteset_oasis_T1(LOAD_TEMP(10) * LOAD_TEMP(11) - 1);
               zextent: 80;
               recolour_mode: RECOLOUR_REMAP;
               palette: LOAD_TEMP(2); } }
spritelayout layout_oasis_T2 {
    ground   { sprite: GROUNDSPRITE_DESERT_1_2; }
    building { sprite: spriteset_oasis_T2(LOAD_TEMP(20) * LOAD_TEMP(21) - 1);
               zextent: 80;
               recolour_mode: RECOLOUR_REMAP;
               palette: LOAD_TEMP(2); } }
spritelayout layout_oasis_T3 {
    ground   { sprite: GROUNDSPRITE_DESERT_1_2; }
    building { sprite: spriteset_oasis_T3(LOAD_TEMP(30) * LOAD_TEMP(31) - 1);
               zextent: 80;
               recolour_mode: RECOLOUR_REMAP;
               palette: LOAD_TEMP(2); } }
spritelayout layout_oasis_T4 {
    ground   { sprite: GROUNDSPRITE_DESERT_1_2; } }

spritelayout layout_oasis_empty {
    ground   { sprite: GROUNDSPRITE_DESERT; } }
spritelayout layout_oasis_grass {
    ground   { sprite: GROUNDSPRITE_DESERT_1_2; } }
spritelayout layout_oasis_palms {
    ground   { sprite: GROUNDSPRITE_DESERT_1_2; }
    building { sprite: spriteset_palms3(LOAD_TEMP(0) + LOAD_TEMP(1)); zextent: 80; } }

switch (FEAT_OBJECTS, SELF, switch_oasis_T1, [
    STORE_TEMP(function_random_bits_from_tile_16_ranges(0, 0), 10),
    STORE_TEMP(function_random_bits_from_tile_32_ranges(0, 1), 11),
    STORE_TEMP(function_cc_recolour(0, 0), 2)])
    { layout_oasis_T1; }
switch (FEAT_OBJECTS, SELF, switch_oasis_T2, [
    STORE_TEMP(function_random_bits_from_tile_16_ranges(0, -1), 20),
    STORE_TEMP(function_random_bits_from_tile_32_ranges(0,  0), 21),
    STORE_TEMP(function_cc_recolour(0, -1), 2)])
    { layout_oasis_T2; }
switch (FEAT_OBJECTS, SELF, switch_oasis_T3, [
    STORE_TEMP(function_random_bits_from_tile_16_ranges(1, -1), 30),
    STORE_TEMP(function_random_bits_from_tile_32_ranges(1,  0), 31),
    STORE_TEMP(function_cc_recolour(1, -1), 2)])
    { layout_oasis_T3; }

switch (FEAT_OBJECTS, SELF, switch_oasis_layout_palms, [
    STORE_TEMP(slope_to_sprite_offset(tile_slope), 0),
    STORE_TEMP(random_ground_32(), 1),
    function_random_bits_from_tile_3_ranges(0, 0)
    ]) {
    0: layout_oasis_empty;
    1: layout_oasis_grass;
    return layout_oasis_palms; }

switch (FEAT_OBJECTS, SELF, switch_oasis_layout_grass, [
    STORE_TEMP(slope_to_sprite_offset(tile_slope), 0),
    STORE_TEMP(random_ground_32(), 1),
    function_random_bits_from_tile_2_ranges(0, 0)
    ]) {
    0: layout_oasis_empty;
    return layout_oasis_grass; }

switch (FEAT_OBJECTS, SELF, switch_oasis, relative_pos) {
    relative_coord(2, 1): switch_oasis_T1;
    relative_coord(2, 2): switch_oasis_T2;
    relative_coord(1, 2): switch_oasis_T3;
    relative_coord(1, 1): layout_oasis_T4;

    relative_coord(0, 0): switch_oasis_layout_palms;
    relative_coord(1, 0): switch_oasis_layout_palms;
    relative_coord(0, 1): switch_oasis_layout_palms;
    relative_coord(2, 0): switch_oasis_layout_palms;
    relative_coord(3, 0): switch_oasis_layout_palms;
    relative_coord(0, 2): switch_oasis_layout_palms;
    relative_coord(0, 3): switch_oasis_layout_palms;
    
    relative_coord(3, 1): switch_oasis_layout_grass;
    relative_coord(3, 2): switch_oasis_layout_grass;
    relative_coord(1, 3): switch_oasis_layout_grass;
    relative_coord(2, 3): switch_oasis_layout_grass;
    relative_coord(3, 3): switch_oasis_layout_grass;
 }

if (game_mode != GAMEMODE_EDITOR) {
    item (FEAT_OBJECTS, oasis, 253){
        property {
            class:                  "SNDY";
            classname:              string(STR_CLASSNAME);
            name:                   string(STR_oasis);
            climates_available:     ALL_CLIMATES;
            size:                   [4,4];
            build_cost_multiplier:  0;
            remove_cost_multiplier: 0;
            introduction_date:      0x00000000;
            end_of_life_date:       0xFFFFFFFF;
            object_flags:           bitmask(OBJ_FLAG_ANYTHING_REMOVE, OBJ_FLAG_ALLOW_BRIDGE, OBJ_FLAG_ONLY_SE);
            height:                 0;
            num_views:              1;
            count_per_map256:       param_random_oases_count;
        }
        graphics {
            default:         switch_oasis;
            purchase:        layout_oasis_menu;
            additional_text: string(STR_autoslope);
            tile_check:      function_check_desert();
        }
    }
}

// // Random rocks at mapgen
spritelayout layout_rocks_D_random_menu {
    ground   { sprite: spriteset_text(6); } }

spritelayout layout_rocks_D_empty {
    ground   { sprite: GROUNDSPRITE_DESERT + LOAD_TEMP(0); } }

spritelayout layout_rocks_D_random {
    // ground   { sprite: spriteset_sand(LOAD_TEMP(0)); }
    ground   { sprite: GROUNDSPRITE_DESERT + LOAD_TEMP(0); }
    building { sprite: spriteset_rocks(LOAD_TEMP(0));
               recolour_mode: RECOLOUR_REMAP;
               palette: rocks_recolour_sprites + 3; } }

switch (FEAT_OBJECTS, SELF, switch_rocks_D_random_layout, [
    STORE_TEMP(slope_to_sprite_offset(tile_slope), 0),
    function_random_bits_from_tile_2_ranges(0, 0)
    ]) {
    0: return layout_rocks_D_empty;
    return layout_rocks_D_random; }

switch (FEAT_OBJECTS, SELF, switch_rocks_D_random, relative_pos) {
    relative_coord(0, 0): switch_rocks_D_random_layout();
    relative_coord(1, 0): switch_rocks_D_random_layout();
    relative_coord(2, 0): switch_rocks_D_random_layout();
    relative_coord(3, 0): switch_rocks_D_random_layout();
    relative_coord(0, 1): switch_rocks_D_random_layout();
    relative_coord(1, 1): switch_rocks_D_random_layout();
    relative_coord(2, 1): switch_rocks_D_random_layout();
    relative_coord(3, 1): switch_rocks_D_random_layout();
    relative_coord(0, 2): switch_rocks_D_random_layout();
    relative_coord(1, 2): switch_rocks_D_random_layout();
    relative_coord(2, 2): switch_rocks_D_random_layout();
    relative_coord(3, 2): switch_rocks_D_random_layout();
    relative_coord(0, 3): switch_rocks_D_random_layout();
    relative_coord(1, 3): switch_rocks_D_random_layout();
    relative_coord(2, 3): switch_rocks_D_random_layout();
    relative_coord(3, 3): switch_rocks_D_random_layout();
}

if (game_mode != GAMEMODE_EDITOR) {
    item (FEAT_OBJECTS, rocks_D_random, 254){
        property {
            class:                  "SNDY";
            classname:              string(STR_CLASSNAME);
            name:                   string(STR_rocks);
            climates_available:     ALL_CLIMATES;
            size:                   [4,4];
            build_cost_multiplier:  0;
            remove_cost_multiplier: 0;
            introduction_date:      0x00000000;
            end_of_life_date:       0xFFFFFFFF;
            object_flags:           bitmask(OBJ_FLAG_ANYTHING_REMOVE, OBJ_FLAG_NO_FOUNDATIONS, OBJ_FLAG_ALLOW_BRIDGE, OBJ_FLAG_ONLY_SE);
            height:                 0;
            num_views:              1;
            count_per_map256:       param_random_rocks_count;
        }
        graphics {
            default:         switch_rocks_D_random;
            purchase:        layout_rocks_D_random_menu;
            tile_check:      function_check_desert();
            autoslope:       return CB_RESULT_AUTOSLOPE;
        }
    }
}